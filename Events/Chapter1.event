
////////////////////////////////////////////////////
//                                                //
// Disassembled with Nintenlord's Event Assembler //
// Version: 11.0.0.0                              //
// Game: FE8                                      //
// File: FE8_clean.gba                            //
// Offset: $9EC66C                                //
//                                                //
////////////////////////////////////////////////////

EventPointerTable(Chapter1Events, PointerList)

ALIGN 4
PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

ALIGN 4
TurnBasedEvents:
TurnEventPlayer(12, AlenaSpawn, 3)
TurnEventPlayer(13, BoroSpawn, 7)
END_MAIN

ALIGN 4
CharacterBasedEvents:
CharacterEventBothWays(7, LoeweIskraTalkCh1, Loewe, Iskra)
CharacterEventBothWays(8, EmilCleoTalkCh1, Emil, Cleo)
CharacterEventBothWays(14, SvetomirAlenaTalkCh1, Svetomir, Alena)
END_MAIN

ALIGN 4
LocationBasedEvents:
Door(3,16)
Door(10,13)
Chest(Mend,3,18)
Chest(SteelBow,10,11)
LOCA 9 LoeweSvetomirFreed [16,10] 0x12
LOCA 10 SennyDoor [16,3] 0x12
LOCA 11 HelmanDoor [10,5] 0x12
Escape(0,EscapeEventCheck,2,0)
Escape(0,EscapeEventCheck,3,0)
END_MAIN

ALIGN 4
MiscBasedEvents:
AREA 0x00 CivilianEscape [14, 0] [15, 0]
CauseGameOverIfLordDies
END_MAIN

ALIGN 4
Dunno1:
END_MAIN

ALIGN 4
Dunno2:
END_MAIN

ALIGN 4
Dunno3:
END_MAIN

ALIGN 4
Tutorial:
END_MAIN

ALIGN 4
Units1:
UNIT Cleo Skyseeker_Dismounted   Cleo Level(2,Ally,False) [17,16] 0x0 0x0 0x0 0x1 [Argilabrys,IronAxe,Vulnerary]      NoAI
UNIT Mira ThiefFighter           Cleo Level(1,Ally,False) [18,16] 0x0 0x0 0x0 0x1 [KillingEdge,Shortsword, Vulnerary] NoAI
UNIT Iskra Mage_F                Cleo Level(1,Ally,False) [18,15] 0x0 0x0 0x0 0x1 [Thunder, Vulnerary]                NoAI
UNIT Emil LanceKnight_Dismounted Cleo Level(1,Ally,False) [18,17] 0x0 0x0 0x0 0x1 [IronLance, Vulnerary]              NoAI
UNIT

ALIGN 4
GreenUnits:
UNIT Loewe Scaleshot_Dismounted Cleo Level(5,NPC,False) [18,10] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Svetomir Priest            Cleo Level(2,NPC,False) [18,12] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Senny Civilian_F_2         0x0 Level(1,NPC,False) [18,3] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Helman Civilian_2          0x0 Level(1,NPC,False) [10,7] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT

ALIGN 4
EnemyUnits:
UNIT Raschwitz_Ch1 BowArmor 0x0      Level(3,Enemy,False) [3,6] 0x0 0x0 0x0 0x0 [SteelBow] NoAI
UNIT 0x70 LanceSoldier Raschwitz_Ch1 Level(1,Enemy,True) [9,15] 0x0 0x0 0x0 0x0 [IronLance] NoAI
UNIT

ALIGN 4
Turn3Reinforcements:
UNIT Alena Myrmidon_F 0x0 Level(3,Enemy,False) [2,14] 0x0 0x0 0x0 0x0 [IronSword, Vulnerary] NoAI
UNIT 0x70 Myrmidon    0x0 Level(1,Enemy,True)  [1,13] 0x0 0x0 0x0 0x0 [IronSword] NoAI
UNIT 0x70 Myrmidon    0x0 Level(1,Enemy,True)  [1,15] 0x0 0x0 0x0 0x0 [IronSword] NoAI
UNIT

ALIGN 4
BoroUnitGroup:
UNIT Boro_Ch1 Champion 0x00 Level(5,Enemy,False) [19,16] 0x0 0x0 0x0 0x0 [MasterLance, SilverLance] NoAI
UNIT

ALIGN 4
Traps1:
RedSigil(2,0)
RedSigil(3,0)
ENDTRAP
ALIGN 4

ALIGN 4
Traps2:
ENDTRAP
ALIGN 4

ALIGN 4
BeginningScene:
LOAD1 1 Units1
ENUN
LOAD1 1 EnemyUnits
ENUN
LOAD1 1 GreenUnits
ENUN
NoFade
ENDA

ALIGN 4
AlenaSpawn:
LOAD1 1 Turn3Reinforcements
ENUN
CAMERA Alena
Text(AlenaSpawnText)
NoFade
ENDA

ALIGN 4
BoroSpawn:
LOAD1 1 BoroUnitGroup
ENUN
CAMERA Boro_Ch1
Text(BoroSpawnText)
NoFade
ENDA

ALIGN 4
LoeweIskraTalkCh1:
Text(LoeweIskraTalkCh1Text)
GiveItem(Kaiserschwert,Loewe)
NoFade
ENDA

ALIGN 4
EmilCleoTalkCh1:
Text(EmilCleoTalkCh1Text)
NoFade
ENDA

ALIGN 4
SvetomirAlenaTalkCh1:
Text(SvetomirAlenaTalkCh1Text)
NoFade
ENDA

ALIGN 4
LoeweSvetomirFreed:
TILECHANGE 3
Text(LoeweFreedText)
GiveItem(IronBow,Loewe)
Text(SvetomirFreedText)
GiveItem(Heal,Svetomir)
NoFade
ENDA

ALIGN 4
SennyDoor:
TILECHANGE 0
Text(SennyFreedText)
NoFade
ENDA

ALIGN 4
HelmanDoor:
TILECHANGE 1
Text(HelmanFreedText)
NoFade
ENDA

ALIGN 4
EscapeEventCheck:
CHECK_ACTIVEID Loewe
BEQ 0 s0 sC
	COUNTER_INC 0
LABEL 0
CHECK_ACTIVEID Cleo
BEQ 1 s0 sC
	COUNTER_INC 0
LABEL 1
COUNTER_CHECK 0
SVAL s1 2
BNE 3 s1 sC
	ENUT 3
	CALL EndingScene
LABEL 3
NoFade
ENDA

ALIGN 4
CivilianEscape:
CHECK_CLASS -1
SVAL s1 Civilian_F_2
BNE 4 s1 sC
	ENUT 0x78
	GOTO 5
LABEL 4
SVAL s1 Civilian_2
BNE 5 s1 sC
	ENUT 0x79
LABEL 5
NoFade
ENDA

ALIGN 4
EndingScene:
EscapeCapturedCheck(Mira, 0x8B)
EscapeCapturedCheck(Iskra, 0x8C)
EscapeCapturedCheck(Emil, 0x8D)
EscapeCapturedCheck(Svetomir, 0x8E)
EscapeCapturedCheck(Alena, 0x8F)
MNC2 Chapter2 // goes to Chapter 2; check the Chapter Definitions file to see what this is
NoFade
ENDA
